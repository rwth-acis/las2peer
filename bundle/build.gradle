plugins {
    id 'java'
    id 'application'
    // plugin for building the las2peer bundle jar
    id "com.github.johnrengelman.shadow" version "5.0.0"
    // maven plugin is used to create .pom files and for publishing
    id 'maven-publish'
}

// disable the default jar, because in the bundle we only want to use the shadowJar
jar.enabled = false

def las2peerRevision = "${project.property('las2peer.revision')}"
def las2peerBuildNumber = "${project.property('las2peer.build.number')}"
def las2peerRelease = System.env.LAS2PEER_RELEASE != null
def las2peerVersion = las2peerRelease ? "$las2peerRevision.$las2peerBuildNumber" : "$las2peerRevision-SNAPSHOT"

group = 'i5'
archivesBaseName = 'las2peer-bundle'
version = las2peerVersion
mainClassName = "i5.las2peer.tools.L2pNodeLauncher"
sourceCompatibility = "${project.property('java.version')}"
targetCompatibility = "${project.property('java.version')}"

repositories {
    // Use JCenter for resolving dependencies.
    jcenter()

    // DBIS Archiva
    maven {
        url "${project.property('dbis.archiva.url')}"
    }
}

dependencies {
    compile project(":core")
    compile project(":restmapper")
    compile project(":webconnector")
}

// put all .jar files into export/jars folder
tasks.withType(Jar) {
    destinationDir = file("$projectDir/export/jars")
}

jar {
    manifest {
        attributes "Main-Class": "i5.las2peer.tools.L2pNodeLauncher"
    }
}

// las2peer bundle jar
shadowJar {
    // remove the "-all" at the end of the artifact / file name
    archiveClassifier = ""

    mergeServiceFiles() 
}

clean.doLast {
    file("export").deleteDir()
}

publishing {
    publications {
        bundle(MavenPublication) {
            from components.java
            artifact shadowJar
            pom {
                artifactId "las2peer-bundle"
                packaging "jar"
            }

            // fix pom file by removing dependencies node
            pom.withXml {
                Node pomNode = asNode()
                pomNode.remove(pomNode.get("dependencies"))
            }
        }
    }

    repositories {
        maven {
            if (las2peerRelease) url = "${project.property('dbis.archiva.url')}"
            else url = "${project.property('dbis.archiva.url.snapshots')}"

            credentials {
                username = System.env.ARCHIVA_USERNAME
                password = System.env.ARCHIVA_PASSWORD
            }
        }
    }
}

// disable module metadata publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}