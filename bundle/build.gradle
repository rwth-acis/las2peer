plugins {
    id 'java'
    id 'application'
    // plugin for building the las2peer bundle jar
    id "com.github.johnrengelman.shadow" version "7.0.0"
    // maven plugin is used to create .pom files and for publishing
    id 'maven-publish'
}

// disable the default jar, because in the bundle we only want to use the shadowJar
jar.enabled = false

def las2peerRevision = "${project.property('las2peer.revision')}"
def las2peerBuildNumber = "${project.property('las2peer.build.number')}"
def las2peerRelease = System.env.LAS2PEER_RELEASE != null
def las2peerVersion = las2peerRelease ? "$las2peerRevision.$las2peerBuildNumber" : "$las2peerRevision-SNAPSHOT"

group = 'io.github.rwth-acis.org.las2peer'
archivesBaseName = 'las2peer-bundle'
version = las2peerVersion
mainClassName = "i5.las2peer.tools.L2pNodeLauncher"
sourceCompatibility = "${project.property('java.version')}"
targetCompatibility = "${project.property('java.version')}"

repositories {
    // Use maven central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation project(":core")
    implementation project(":restmapper")
    implementation project(":webconnector")
}

// put all .jar files into export/jars folder
tasks.withType(Jar) {
    destinationDirectory = file("$projectDir/export/jars")
}

jar {
    manifest {
        attributes(
            "Main-Class": "i5.las2peer.tools.L2pNodeLauncher",
            "Add-Opens": "java.base/java.lang java.base/java.util"
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

// las2peer bundle jar
shadowJar {
    // remove the "-all" at the end of the artifact / file name
    archiveClassifier = ""

    mergeServiceFiles() 
}

clean.doLast {
    file("export").deleteDir()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/rwth-acis/las2peer")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
            artifact shadowJar
            pom {
                name = 'las2peer-bundle'
                artifactId = 'las2peer-bundle'
                description = 'A bundle for the las2peer project'
                url = 'https://las2peer.org/'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/rwth-acis/las2peer?tab=License-1-ov-file#readme'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'rwth-acis'
                        email= 'las2peer@dbis.rwth-aachen.de'
                        organization = 'rwth-acis'
                        organizationUrl = 'https://las2peer.org/'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/rwth-acis/las2peer.git'
                    developerConnection = 'scm:git:ssh://git@github.com:rwth-acis/las2peer.git'
                    url = 'https://github.com/rwth-acis/las2peer'
                }
            }

            // fix pom file by removing dependencies node
            pom.withXml {
                def packagingNode = asNode().appendNode('packaging', 'jar')

                Node pomNode = asNode()
                pomNode.remove(pomNode.get("dependencies"))
            }
        }
    }
}

// disable module metadata publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}